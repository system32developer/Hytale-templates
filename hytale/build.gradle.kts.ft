import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import java.nio.file.Files
import java.nio.file.StandardCopyOption

plugins {
    kotlin("jvm") version "${KOTLIN_VERSION}"
    id("com.gradleup.shadow") version "8.3.0"
}

val hytaleHome = "${System.getProperty("user.home")}/AppData/Roaming/Hytale"
val hytaleServer = "install/release/package/game/latest"

group = "${BUILD_COORDS.groupId}"
version = "${BUILD_COORDS.version}"

repositories {
    mavenCentral()
}

dependencies {
    compileOnly(files("$hytaleHome/$hytaleServer/Server/HytaleServer.jar"))
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
}

tasks.shadowJar {
    archiveClassifier.set("")
}

tasks.register<Copy>("buildAndCopyToRunMods") {
    dependsOn(tasks.named<ShadowJar>("shadowJar"))

    val runDir = file("run")
    if (!runDir.exists()) runDir.mkdirs()
    val runModsDir = file("run/mods")
    val runServerDir = file("run/Server")

    val hytaleHomeDir = file(hytaleHome)

    val assetsSource = hytaleHomeDir.resolve("$hytaleServer/Assets.zip")
    val serverJarSource = hytaleHomeDir.resolve("$hytaleServer/Server/HytaleServer.jar")

    val serverAotSource = hytaleHomeDir.resolve("$hytaleServer/Server/HytaleServer.aot")

    val assetsTarget = runDir.resolve("Assets.zip")
    val serverJarTarget = runServerDir.resolve("HytaleServer.jar")
    val serverAotTarget = runServerDir.resolve("HytaleServer.aot")

    from(tasks.named<ShadowJar>("shadowJar").flatMap { it.archiveFile })
    into(runModsDir)

    doFirst {
        runModsDir.mkdirs()
        runServerDir.mkdirs()

        fun requireOrCopy(source: File, target: File, name: String) {
            if (!target.exists()) {
                if (!source.exists()) {
                    error("‚ùå Not found $name on /run neither Hytale Home:\n${source.absolutePath}")
                }
                println("üì¶ Copying $name from Hytale Home")
                Files.copy(
                    source.toPath(),
                    target.toPath(),
                    StandardCopyOption.REPLACE_EXISTING
                )
            }
        }

        requireOrCopy(assetsSource, assetsTarget, "Assets.zip")
        requireOrCopy(serverJarSource, serverJarTarget, "HytaleServer.jar")
        requireOrCopy(serverAotSource, serverAotTarget, "HytaleServer.aot")
    }
}

